apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace 'org.librepdf.openpdf.android.awtcompat'
    compileSdkVersion 36
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant("release") {}
    }
    ndkVersion "28.2.13676358"
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    // forced to use java8, java9+ has module conflicts with java.base that cannot be overridden
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api 'com.jaredrummler:sfntly:1.0.1'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

project.afterEvaluate {
    publishing {
        publications {
            repositories.maven {
                url 'https://andob.io/repository/open_source/'
                credentials {
                    username "$System.env.MAVEN_PUBLISH_USERNAME"
                    password "$System.env.MAVEN_PUBLISH_PASSWORD"
                }
            }

            release(MavenPublication) {
                from components.release
                groupId 'ro.andob.androidawt'
                artifactId 'androidawt'
                version '1.0.4'
            }
        }
    }
}
